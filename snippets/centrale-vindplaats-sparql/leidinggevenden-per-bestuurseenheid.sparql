PREFIX lblodlg: <http://data.lblod.info/vocabularies/leidinggevenden/>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX org:     <http://www.w3.org/ns/org#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>

SELECT DISTINCT ?bestuurseenheidNaam ?roleLabel ?persoonUri ?gebruikteVoornaam ?achternaam
WHERE {
  ?bestuursorgaanUri besluit:bestuurt ?bestuurseenheidUri .
  ?bestuurseenheidUri skos:prefLabel ?bestuurseenheidNaam .

  ?bestuursorgaanInPeriodeUri a besluit:Bestuursorgaan ;
    mandaat:isTijdspecialisatieVan ?bestuursorgaanUri ;
    lblodlg:heeftBestuursfunctie ?bestuursfunctieUri .

  ?bestuursfunctieUri org:role ?roleUri .
  ?roleUri skos:prefLabel ?roleLabel .

  ?mandatarisUri org:holds ?bestuursfunctieUri ;
    mandaat:isBestuurlijkeAliasVan ?persoonUri .

  ?persoonUri persoon:gebruikteVoornaam ?gebruikteVoornaam ;
    foaf:familyName ?achternaam .
}

