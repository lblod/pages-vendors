PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX org:     <http://www.w3.org/ns/org#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>

SELECT DISTINCT ?naam ?achternaam ?roleLabel ?bestuursorgaanNaam ?start ?einde
WHERE {
  <http://data.lblod.info/id/mandatarissen/5C46EC3357753A000900007B>
    mandaat:isBestuurlijkeAliasVan ?persoonUri .
  ?persoonUri persoon:gebruikteVoornaam ?naam ;
    foaf:familyName ?achternaam .

  ?mandaatUri org:role ?roleUri .
  ?roleUri skos:prefLabel ?roleLabel .

  ?mandatarisUri org:holds ?mandaatUri ;
    mandaat:isBestuurlijkeAliasVan ?persoonUri .

  ?bestuursorgaanInPeriodeUri a besluit:Bestuursorgaan ;
    org:hasPost ?mandaatUri .

  ?bestuursorgaanInPeriodeUri a besluit:Bestuursorgaan ;
    mandaat:isTijdspecialisatieVan ?bestuursorgaanUri ;
    mandaat:bindingStart ?start ;
    org:hasPost ?mandaatUri .

  ?bestuursorgaanUri skos:prefLabel ?bestuursorgaanNaam .
  OPTIONAL { ?bestuursorgaanInPeriodeUri mandaat:bindingEinde ?einde . }
  FILTER ( ?mandatarisUri NOT IN ( <http://data.lblod.info/id/mandatarissen/5C46EC3357753A000900007B> ))
}
